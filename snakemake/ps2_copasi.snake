# snakemake pipeline
#
# This file is part of sbpipe.
#
# sbpipe is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# sbpipe is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with sbpipe.  If not, see <http://www.gnu.org/licenses/>.
#
#
#
# $Revision: 1.0 $
# $Author: Piero Dalle Pezze $
# $Date: 2017-01-23 23:43:32 $


# CALL: snakemake -s ps2_copasi.snake --configfile FILE
# where FILE is a config.yaml file


import sys
import os

SBPIPE = os.environ["SBPIPE"]

MODEL           = os.path.join("Models", config['model'])
MODEL_NAME      = config['model'].split('.')[0]
REPORT_PREFIX   = "report_ps2_"
WORKING_FOLDER  = os.path.join("Working_Folder", MODEL_NAME)
PREPROC_FOLDER  = "ps2_preproc"
POSTPROC_FOLDER = "ps2_postproc"
PLOTS_FOLDER    = "ps2_plots"

MODEL_EXT       = "." + config['model'].split('.')[1]
CSV_EXT         = ".csv"
PNG_EXT         = ".png"
LATEX_EXT       = ".tex"
PDF_EXT         = ".pdf"
LOG_EXT         = ".log"


rule all:
    input:
        plot=os.path.join(WORKING_FOLDER, PLOTS_FOLDER, MODEL_NAME + "__eval_Insulin__rep_1__tp_0" + PNG_EXT),
        report=os.path.join(WORKING_FOLDER, REPORT_PREFIX + MODEL_NAME + ".pdf")


rule preproc:
    input:
        file=MODEL
    output:
        file=os.path.join(WORKING_FOLDER, PREPROC_FOLDER, MODEL_NAME + "_1" + MODEL_EXT)
    log:
        file=MODEL_NAME + LOG_EXT
    run:
        SBPIPE = os.environ["SBPIPE"]
        sys.path.insert(0, SBPIPE)
        from sbpipe.snakemake.preproc import copasi_preproc
        copasi_preproc(input.file, output.file)


rule gen_data:
    input:
        file=os.path.join(WORKING_FOLDER, PREPROC_FOLDER, MODEL_NAME + "_1" + MODEL_EXT)
    output:
        file=os.path.join(WORKING_FOLDER, PREPROC_FOLDER, MODEL_NAME + "_1" + CSV_EXT)
    log:
        file=MODEL_NAME + LOG_EXT
    run:
        SBPIPE = os.environ["SBPIPE"]
        sys.path.insert(0, SBPIPE)
        from sbpipe.snakemake.generate_data import run_copasi_model
        run_copasi_model(input.file)


rule ps2_postproc:
    input:
        file=os.path.join(WORKING_FOLDER, PREPROC_FOLDER, MODEL_NAME + "_1" + CSV_EXT)
    output:
        file=os.path.join(WORKING_FOLDER, POSTPROC_FOLDER, MODEL_NAME + "_1" + CSV_EXT)
    params:
        sim_length=config["sim_length"],
        runs=config["runs"]
    log:
        file=MODEL_NAME + LOG_EXT
    run:
        SBPIPE = os.environ["SBPIPE"]
        sys.path.insert(0, SBPIPE)
        from sbpipe.snakemake.ps2_postproc import generic_postproc
        generic_postproc(input.file, output.file, params.sim_length)


rule ps2_analysis:
    input:
        file=os.path.join(WORKING_FOLDER, POSTPROC_FOLDER, MODEL_NAME + "_1" + CSV_EXT)
    output:
        file=os.path.join(WORKING_FOLDER, PLOTS_FOLDER, MODEL_NAME + "__eval_Insulin__rep_1__tp_0" + PNG_EXT)
    params:
        rscript=os.path.join(SBPIPE, 'sbpipe', 'R', 'sbpipe_ps2_main.r'),
        model_name=MODEL_NAME,
        postproc_dir=os.path.join(WORKING_FOLDER, POSTPROC_FOLDER),
        scanned_par1=config["scanned_par1"],
        scanned_par2=config["scanned_par2"],
        plots_dir=os.path.join(WORKING_FOLDER, PLOTS_FOLDER),
        runs=config["runs"]
    log:
        file=MODEL_NAME + LOG_EXT
    shell:
        # NOTE: for this analysis, one can iterate over params.runs
        "Rscript --vanilla {params.rscript} {params.model_name} "
        "{params.scanned_par1} {params.scanned_par2} {params.postproc_dir} {params.plots_dir} {params.runs}"


rule ps2_latex_report:
    input:
        file=os.path.join(WORKING_FOLDER, PLOTS_FOLDER, MODEL_NAME + "__eval_Insulin__rep_1__tp_0" + PNG_EXT)
    output:
        file=os.path.join(WORKING_FOLDER, REPORT_PREFIX + MODEL_NAME + LATEX_EXT)
    params:
        working_folder_dir=WORKING_FOLDER,
        plots_folder=PLOTS_FOLDER,
        report_prefix=REPORT_PREFIX,
        model_name=MODEL_NAME,
        scanned_par1=config["scanned_par1"],
        scanned_par2=config["scanned_par2"]
    log:
        file=MODEL_NAME + LOG_EXT
    run:
        SBPIPE = os.environ["SBPIPE"]
        sys.path.insert(0, SBPIPE)
        from sbpipe.report.latex_reports import latex_report_ps2
        latex_report_ps2(params.working_folder_dir, params.plots_folder, params.report_prefix, params.model_name,
                     params.scanned_par1, params.scanned_par2)


rule ps2_pdf_report:
    input:
        file=os.path.join(WORKING_FOLDER, REPORT_PREFIX + MODEL_NAME + LATEX_EXT)
    output:
        file=os.path.join(WORKING_FOLDER, REPORT_PREFIX + MODEL_NAME + PDF_EXT)
    params:
        working_folder_dir=WORKING_FOLDER,
        latex_file=REPORT_PREFIX + MODEL_NAME + LATEX_EXT
    log:
        file=MODEL_NAME + LOG_EXT
    run:
        SBPIPE = os.environ["SBPIPE"]
        sys.path.insert(0, SBPIPE)
        from sbpipe.report.latex_reports import pdf_report
        pdf_report(params.working_folder_dir, params.latex_file)

